# Copyright (C)2008 Laurence Tratt http:#tratt.net/laurie/
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to
# deal in the Software without restriction, including without limitation the
# rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
# sell copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
# IN THE SOFTWARE.


AC_INIT([extsmail], EXTSMAIL_VERSION)

################################################################################
# Generic initialization
#

AC_CONFIG_HEADER(Config.h)

AC_LANG(C)

AC_PROG_CC
AC_PROG_LEX
AC_PROG_SED
AC_ISC_POSIX
AC_PROG_GCC_TRADITIONAL
AC_GNU_SOURCE


###############################################################################
# Function / feature checks
#

CFLAGS="$CFLAGS -std=c99 -Wall -pedantic -Wextra"

# kqueue (*BSD)

AH_TEMPLATE(HAVE_KQUEUE,
  [Define if your platform has the kqueue family of functions.])
AC_CHECK_FUNC(kqueue, [AC_DEFINE(HAVE_KQUEUE)])

# inotify (Linux)

AH_TEMPLATE(HAVE_INOTIFY,
  [Define if your platform has the inotify family of functions.])
AC_CHECK_HEADERS(sys/inotify.h, [AC_DEFINE(HAVE_INOTIFY)])

# fileno (Linux)

case `uname -s` in
  Linux* ) CFLAGS="$CFLAGS -D_POSIX_C_SOURCE=2";;
esac

# set hardening flags

LDFLAGS="$LDFLAGS -Wl,-z,relro"

# strtonum

AH_TEMPLATE(HAVE_STRTONUM, [Define if your platform has the strtonum function.])
AC_CHECK_FUNC(strtonum, [AC_DEFINE(HAVE_STRTONUM)], [AC_SUBST(COMPAT_STRTONUM,
                                                     compat/strtonum.o)])

# yylex_destroy
AH_TEMPLATE([HAVE_YYLEX_DESTROY],
            [Define if lex/flex has yylex_destroy])
AC_MSG_CHECKING([whether yylex_destroy is generated by flex])
flex_version=`$LEX -V -v --version 2>/dev/null | $SED -e 's/^.* //'`
case $flex_version in
    [[0-1].*] | [2.[0-4].*] | [2.5.[0-8]] | [2.5.[0-8][A-Za-z]*])
        AC_MSG_RESULT([no])
        ;;
    *)
        AC_MSG_RESULT([yes])
        AC_DEFINE([HAVE_YYLEX_DESTROY])
        ;;
esac

################################################################################
# Output
#

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
